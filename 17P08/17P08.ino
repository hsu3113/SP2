#include <Servo.h>

// Arduino pin assignment
#define PIN_IR    0        // IR sensor at Pin A0
#define PIN_LED   9
#define PIN_SERVO 10

#define _DUTY_MIN 500     // servo full clock-wise position (0 degree)
#define _DUTY_NEU 1500     // servo neutral position (90 degree)
#define _DUTY_MAX 2500     // servo full counter-clockwise position (180 degree)

#define _DIST_MIN  100.0   // minimum distance 100mm
#define _DIST_MAX  250.0   // maximum distance 250mm

#define EMA_ALPHA  0.1     // EMA Filter alpha value
#define LOOP_INTERVAL 20   // Loop interval in milliseconds

Servo myservo;
unsigned long last_loop_time = 0;  // in milliseconds

float dist_prev = _DIST_MIN;
float dist_ema = _DIST_MIN;

void setup() {
  pinMode(PIN_LED, OUTPUT);
  
  myservo.attach(PIN_SERVO); 
  myservo.writeMicroseconds(_DUTY_NEU);
  
  Serial.begin(1000000);    // Set serial baud rate to 1,000,000 bps
}

void loop() {
  unsigned long time_curr = millis();
  int duty;
  float a_value, dist_raw;

  // Wait until the next event time
  if (time_curr < (last_loop_time + LOOP_INTERVAL))
    return;
  last_loop_time += LOOP_INTERVAL;

  // Read IR sensor value and calculate raw distance
  a_value = analogRead(PIN_IR);
  dist_raw = ((6762.0 / (a_value - 9.0)) - 4.0) * 10.0;

  // Apply range filter (only process if in range)
  if (dist_raw >= _DIST_MIN && dist_raw <= _DIST_MAX) {
    digitalWrite(PIN_LED, LOW);  // Turn on LED if within range
  } else {
    digitalWrite(PIN_LED, HIGH);   // Turn off LED if out of range
  }

  // Apply EMA filter
  dist_ema = EMA_ALPHA * dist_raw + (1 - EMA_ALPHA) * dist_prev;
  dist_prev = dist_ema;

  // Map distance to duty cycle manually without map() function
  duty = dist_ema * 2000 / 150 - 800;

  if (duty < _DUTY_MIN) {
    duty = _DUTY_MIN;
  }
  if (duty > _DUTY_MAX) {
    duty = _DUTY_MAX;
  }

  // Set servo position based on duty
  myservo.writeMicroseconds(duty);
  // Print debug information
  Serial.print("_DUTY_MIN:");  Serial.print(_DUTY_MIN);
  Serial.print("_DIST_MIN:");  Serial.print(_DIST_MIN);
  Serial.print(",IR:");        Serial.print(a_value);
  Serial.print(",dist_raw:");  Serial.print(dist_raw);
  Serial.print(",ema:");       Serial.print(dist_ema);
  Serial.print(",servo:");     Serial.print(duty);
  Serial.print(",_DIST_MAX:"); Serial.print(_DIST_MAX);
  Serial.print(",_DUTY_MAX:"); Serial.print(_DUTY_MAX);
  Serial.println("");
}
